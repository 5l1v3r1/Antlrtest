Protocol: EPMO

Types:
	Agent B,C,M;
	Number NxC,NxM,NxB,Goods,Price,one,two,three,four,five,six,empty;
	Function id,pk,sk,hmac,hash

Knowledge:
	B: B,C,M,one,two,three,four,five,six,inv(pk(B)),inv(sk(B)),empty,pk,sk,hmac,hash;
	C: B,C,M,one,two,three,four,five,six,inv(pk(C)),inv(sk(C)),empty,pk,sk,hmac,hash;
	M: B,C,M,one,two,three,four,five,six,inv(pk(M)),inv(sk(M)),empty,pk,sk,hmac,hash
	where B!=C, B!=M, C!=M


Actions:
	C *->* M: Goods,Price
	M *->* C: empty
	C -> M: one,{C,NxC,Goods,Price}pk(M)
	M -> C: two,{NxC,NxM,M,Goods,Price}pk(C)
	C -> B: three,{C,NxC,NxM,Price}pk(B)
	B -> C: four,{hash(C,NxC,NxB,NxM,Price)}inv(sk(B)),{NxC,NxB}pk(C)
	C -> M: five,{hash(C,NxC,NxB,NxM,Price)}inv(sk(B)),NxB
	M -> B: six,{hash(B,NxB,NxM)}inv(sk(M))

Goals:
	M authenticates C on Goods,Price,B
	B authenticates C on Price,B,M
	inv(pk(B)) secret between B
	inv(sk(B)) secret between B
	inv(pk(C)) secret between C
	inv(sk(C)) secret between C
	inv(pk(M)) secret between M
	inv(sk(M)) secret between M